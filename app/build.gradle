apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.full.dialer.top.secure.encrypted"
        minSdkVersion 23
        targetSdkVersion 32

        versionCode versionMajor*10000+versionMinor*100+versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        resValue "string","app_version", "${defaultConfig.versionName}"
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                output.outputFileName = "FullDialer-v${android.defaultConfig.versionName}-${variant.buildType.name}.apk"
            }
        }

        setProperty("archivesBaseName", "dialer")
    }

    signingConfigs {
        release {
            storeFile file('../keys/keys.jks')
            storePassword System.getenv("FULLDIVE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("FULLDIVE_ALIAS")
            keyPassword System.getenv("FULLDIVE_ALIAS_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {

        }
    }

    flavorDimensions "variants"
    productFlavors {
        core {}
        fdroid {}
        prepaid {}
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation project(':commons')
   // implementation 'com.github.SimpleMobileTools:Simple-Commons:4a71789f06'
    implementation 'com.github.tibbi:IndicatorFastScroll:4524cd0b61'
    implementation 'me.grantland:autofittextview:0.2.1'
    implementation "com.fulldive:popups:1.0.13"
}
